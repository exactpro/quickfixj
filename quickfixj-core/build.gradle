/******************************************************************************
 * Copyright 2009-2018 Exactpro (Exactpro Systems Limited)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/

apply plugin: 'artifactory'
apply plugin: 'provided-base'

ext {
    dictDir = file('src/main/dicts')
}

archivesBaseName = "quickfixj-core"

// Change default project layout
sourceSets {
    main {
        java {
            srcDir genJavaDir
        }
    }
    test {
        resources {
            srcDir "${dictDir}"
        }
    }
}

dependencies {
    if (incremental_build) {
        compile 'com.exactpro.quickfixj:quickfixj-code-generator:1.6.0.1-SNAPSHOT'
    } else {
        compile project(':quickfixj-codegenerator')
    }
    
    compile 'com.exactpro.mina:apache-mina-core:2.0.9.1'

    compile "org.slf4j:slf4j-api:${version_slf4j}"

    compile 'com.cloudhopper.proxool:proxool:0.9.1'

    //Using for SleepycatStore
    compile 'berkeleydb:je:2.1.30'

    testCompile 'junit:junit:4.10'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.hamcrest:hamcrest-all:1.1'
    testCompile 'hsqldb:hsqldb:1.8.0.10'
    testCompile 'tyrex:tyrex:1.0.1'
    testCompile "org.slf4j:slf4j-jdk14:${version_slf4j}"
}

task generateMessage {
	inputs.dir dictDir
	outputs.dir genJavaDir

    doLast {
        javaexec {
            classpath sourceSets.main.runtimeClasspath
            main 'org.quickfixj.codegenerator.MessageCodeGenerator'
            args = ["${dictDir}", './', genJavaDir]
            jvmArgs = ['-Dgenerator.decimal=true',  '-Dgenerator.includeMicroseconds=true']
        }
    }
}

compileJava.dependsOn.add('generateMessage')

test {

    // some jre versions are disable 'MD5' algorithm in it's 'java.security' file
    jvmArgs "-Djava.security.properties=${projectDir.absolutePath}/src/test/resources/java.security"
    jvmArgs '-XX:MaxPermSize=128m'

    systemProperties 'atest.heartbeat' : '5'
    systemProperties 'atest.timeout' : '60000'
    systemProperties 'atest.reconnectDelay' : '5'

    exclude '**/*ForTest.class'
    exclude '**/Abstract*Test.class'
    exclude '**/AcceptanceTestSuite$*'

    println "Test args ${allJvmArgs}"
    //uncomment for logging test time
    /*afterTest { desc, result ->
        println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}. Time: ${(result.endTime - result.startTime)/1000}s"
    }*/
    testLogging.showStandardStreams = true
}